; Requirements.
!include "MUI2.nsh"
!include "Sections.nsh"

; Configuration.
!define PNAME       "First Snow"
!define PAUTHOR     "Salty Salty Studios"
!define PEXECUTABLE "First Snow.exe"
!define PFILES      "../../dists/FirstSnow-win"
!define PRESOURCES  "."
!ifndef PVERSION
!define PVERSION    "1.0.0.0"
!endif
!define /date PYEAR "%Y"

; Metadata.
Unicode true
Name "${PNAME}"
OutFile "setup.exe"
InstallDir "$PROGRAMFILES\${PNAME}"
RequestExecutionLevel admin
SetCompressor /SOLID lzma
BrandingText " "

VIProductVersion "${PVERSION}"
VIAddVersionKey "ProductName"     "${PNAME}"
VIAddVersionKey "FileVersion"     "${PVERSION}"
VIAddVersionKey "ProductVersion"  "${PVERSION}"
VIAddVersionKey "CompanyName"     "${PAUTHOR}"
VIAddVersionKey "LegalCopyright"  "Â© ${PYEAR} ${PAUTHOR}"
VIAddVersionKey "FileDescription" "${PNAME} installer"

; Functions.
Function create_shortcut
    CreateShortCut "$DESKTOP\${PNAME}.lnk" "$INSTDIR\${PEXECUTABLE}" "" "$INSTDIR\${PEXECUTABLE}" 0
FunctionEnd

; Definitions.

; UI.
!define MUI_ICON "${PRESOURCES}/install.ico"
!define MUI_UNICON "${PRESOURCES}/uninstall.ico"
!define MUI_WELCOMEFINISHPAGE_BITMAP "${PRESOURCES}/banner.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "${PRESOURCES}/banner.bmp"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "${PRESOURCES}/logo.bmp"
!define MUI_ABORTWARNING
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN "$INSTDIR\${PEXECUTABLE}"
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!define MUI_FINISHPAGE_SHOWREADME ""
!define MUI_FINISHPAGE_SHOWREADME_CHECKED
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Create desktop shortcut"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION create_shortcut

; UI pages.
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${PRESOURCES}/COPYING"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH
!insertmacro MUI_LANGUAGE "English"

; Installation stuff.
Section "${PNAME}" SecProgram
  SetOutPath "$INSTDIR"

  File /r ${PFILES}/*.*
  WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

Section "Uninstall"
  Delete "$INSTDIR\Uninstall.exe"
  Delete "$SMPROGRAMS\${PNAME}.lnk"
  Delete "$DESKTOP\${PNAME}.lnk"
  RMDir /r "$INSTDIR"
SectionEnd
